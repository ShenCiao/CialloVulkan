#version 460
#extension GL_EXT_debug_printf : enable

layout(local_size_x = 1024) in;


struct Vertex {
    vec2 pos;
    float width;
    float _pad0;
    vec4 color;
};

layout(binding = 0) buffer Input{
    Vertex[] inVertices;
};

layout(binding = 1) buffer Output{
    Vertex[] outVertices;
};

layout(binding = 2) buffer DrawIndirectCommand{
    uint vertexCount;
    uint instanceCount;
    uint firstVertex;
    uint firstInstance;
} draw;

layout(binding = 3) uniform _Spacing {float spacing;};

shared float[gl_WorkGroupSize.x * 2] segmentLengths;
shared uint n_totDots;

void main(){
    uint id = gl_LocalInvocationID.x;
    vec2 p0 = inVertices[id].pos;
    vec2 p1 = id == 0 ? p0:inVertices[id - 1].pos;
    // length of each segment
    float curLength = distance(p0, p1);
    segmentLengths[id] = curLength;

    barrier();
	memoryBarrierShared();

    // Prefix sum on segment length
    const uint n_steps = uint(log2(gl_WorkGroupSize.x)) + 1;

    for(uint s = 0; s < n_steps; s++){
        uint mask = (1 << s) - 1;
		uint rd_id = ((id >> s) << (s + 1)) + mask;
		uint wr_id = rd_id + 1 + (id & mask);
		
		segmentLengths[wr_id] += segmentLengths[rd_id];
		
		barrier();
		memoryBarrierShared();
    }
    // Prefix sum done! For now segmentLengths is the distance from the starting point of polyline

    float dotDistance = spacing;
    
    // Only invocation id within [0, inVertices.length()) supposed to be dealed with
    if (id != 0 && id < inVertices.length()) {
        float tDistance = dotDistance - mod(segmentLengths[id - 1], dotDistance);
        uint idOut = uint( segmentLengths[id - 1]/dotDistance );

        while (tDistance + segmentLengths[id - 1] < segmentLengths[id]){
            idOut += 1;
            float t = tDistance/curLength;
            // interpolate everything with t value
            outVertices[idOut].pos = mix(inVertices[id-1].pos, inVertices[id].pos, t);
            outVertices[idOut].color = mix(inVertices[id-1].color, inVertices[id].color, t);
            outVertices[idOut].width = mix(inVertices[id-1].width, inVertices[id].width, t);

            tDistance += dotDistance;
        }

        if (id == inVertices.length() - 1) // endpoint of poyline
        {
            // Pad one dot at the end of stroke and idOut start with zero
            draw.vertexCount = idOut+2;
            outVertices[idOut+1].pos = outVertices[idOut].pos + inVertices[id].pos - inVertices[id-1].pos;
            outVertices[idOut+1].color = outVertices[idOut].color;
            outVertices[idOut+1].width = outVertices[idOut].width;
        }
    }
    else if(id == 0){
        outVertices[0].pos = inVertices[0].pos;
        outVertices[0].color = inVertices[0].color;
        outVertices[0].width = inVertices[0].width;

        draw.instanceCount = 1;
        draw.firstVertex = 0;
        draw.firstInstance = 0;
    }
}